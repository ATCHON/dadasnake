def get_fastq(wildcards):
    return os.path.expandvars(config['raw_directory'])+"/"+samples.loc[(wildcards.library,wildcards.run), ["r1_file"]].dropna()

def get_lib_perRunAndSample(wildcards,prefix,suffix):
    return prefix+samples.loc[(samples['run']==wildcards.run) & (samples['sample']==wildcards.sample), "library"].unique()+suffix

#ruleorder: cut_primer_both > combine_or_rename 

rule primers_control:
    input:
        expand("preprocessing/{samples.run}/{samples.sample}.fastq", samples=samples.itertuples()),
        "reporting/readNumbers.tsv",
        "reporting/primerNumbers_perSample.tsv"
    output:
        "primers.done"
    threads: 1
    params:
        runtime="00:10:00",
        mem="8G"
    shell:
        """
        touch {output}
        """

rule combine_or_rename:
    input:
        "reporting/primerNumbers_perLibrary.tsv",
        files = lambda wildcards: get_lib_perRunAndSample(wildcards,"preprocessing/{run}/",".fastq")    
    output:
        "preprocessing/{run}/{sample}.fastq"
    wildcard_constraints:
        sample='|'.join(samples['sample'])
    threads: 1
    log: "logs/combine_or_rename.{run}.{sample}.log"
    params:
        runtime="01:00:00",
        mem="8G"
    run:
        if len(input) > 2:
            shell("cat {input.files} > {output}")
        else:
            shell("mv {input.files} {output}")

rule input_numbers:
    input:
        "reporting/sample_table.tsv",
        expand("{raw_directory}/{file}", file=samples.r1_file,raw_directory=os.path.expandvars(config['raw_directory']))
    output:
        report("reporting/readNumbers.tsv",category="Reads")
    threads: 1
    params:
        currentStep = "raw",
        runtime="12:00:00",
        mem="8G"
    conda: "dada_env_new.yml"
    log: "logs/countInputReads.log"
    script:
        "{config[dada_src]}/report_readNumbers.single.R" 


rule primer_numbers:
    input:
        "reporting/readNumbers.tsv",
        expand("preprocessing/{samples.run}/{samples.library}.fastq", samples=samples.itertuples())
    output:
        report("reporting/primerNumbers_perLibrary.tsv",category="Reads"),
        report("reporting/primerNumbers_perSample.tsv",category="Reads")
    threads: 1
    params:
        currentStep = "primers",
        runtime="12:00:00",
        mem="8G"
    log: "logs/countPrimerReads.log"
    conda: "dada_env_new.yml"
    script:
        "{config[dada_src]}/report_readNumbers.single.R"


#script to visualize read numbers -> run once for all steps


if config['sequencing_direction'] == "fwd_1":
    rule cut_primer_both:
        input:
            get_fastq
        output:
            "preprocessing/{run}/{library}.fastq"
#        wildcard_constraints:
#            library='|'.join(samples['library'])
        threads: 1
        params:
            runtime="12:00:00",
            mem="8G"
        log: "logs/cutadapt.{run}.{library}.log"
        message: "Running {config[dada_bin]}/cutadapt on {input}. Assuming forward primer is in read {config[primers][fwd][sequence]}"
        shell:
            """
            TMPD=$(mktemp -d -t --tmpdir={TMPDIR} 'XXXXXX') 
            FWD_RC=`echo {config[primers][fwd][sequence]} | tr '[ATUGCYRSWKMBDHNatugcyrswkbdhvn]' '[TACGRYSWMKVHDBNtaacgryswmkvhdbn]' |rev`
            RVS_RC=`echo {config[primers][rvs][sequence]} | tr '[ATUGCYRSWKMBDHNatugcyrswkbdhvn]' '[TAACGRYSWMKVHDBNtaacgryswmkvhdbn]' |rev`
                
            {config[dada_bin]}/cutadapt -g {config[primers][fwd][sequence]} \
            --no-indels -n {config[primer_cutting][count]} -O {config[primer_cutting][overlap]} \
            -m 1 \
            -j {threads} -e {config[primer_cutting][perc_mismatch]} --trimmed-only \
            -o $TMPD/{wildcards.library}.fastq {input} &> {log}

            {config[dada_bin]}/cutadapt -a $RVS_RC \
             --no-indels -n {config[primer_cutting][count]} \
             -m 1 \
             -j {threads} -e {config[primer_cutting][perc_mismatch]} \
             -o {output} $TMPD/{wildcards.library}.fastq >> {log} 2>&1
             """

elif config['sequencing_direction'] == "rvs_1":
    rule cut_primers_both:
        input:
            get_fastq
        output:
            "preprocessing/{run}/{library}.fastq",
        threads: 1
        params:
            runtime="12:00:00",
            mem="8G"
        log: "logs/cutadapt.{run}.{library}.log"
        message: "Running {config[dada_bin]}/cutadapt on {input}. Assuming reverse primer is in read."
        shell:
            """
            TMPD=$(mktemp -d -t --tmpdir={TMPDIR} "XXXXXX")

            FWD_RC=`echo {config[primers][fwd][sequence]} | tr '[ATUGCYRSWKMBDHNatugcyrswkbdhvn]' '[TAACGRYSWMKVHDBNtaacgryswmkvhdbn]' |rev`
            RVS_RC=`echo {config[primers][rvs][sequence]} | tr '[ATUGCYRSWKMBDHNatugcyrswkbdhvn]' '[TAACGRYSWMKVHDBNtaacgryswmkvhdbn]' |rev`

            {config[dada_bin]}/cutadapt -g {config[primers][rvs][sequence]} \
             --no-indels -n {config[primer_cutting][count]} -O {config[primer_cutting][overlap]} \
              -m 1\
             -j {threads} -e {config[primer_cutting][perc_mismatch]} --trimmed-only \
             -o $TMPD/{wildcards.library}.fastq {input} &> {log}

            {config[dada_bin]}/cutadapt -a $FWD_RC \
             --no-indels -n {config[primer_cutting][count]} \
              -m 1 \
             -j {threads} -e {config[primer_cutting][perc_mismatch]} \
             -o {output} $TMPD/{wildcards.library}.fastq >> {log}  2>&1
            """

else:
    rule cut_primers_both:
        input:
            get_fastq
        output:
            "preprocessing/{run}/{library}.fastq"
        threads: 1
        params:
            runtime="12:00:00",
            mem="8G"
        log: "logs/cutadapt.{run}.{library}.log"
        message: "Running {config[dada_bin]}/cutadapt on {input}. Searching for both primers.\n Note that this step does not check for the direction, so if your libraries were not sequenced with the same direction, this will not turn them."
        shell:
            """
            TMPD=$(mktemp -d -t --tmpdir={TMPDIR} "XXXXXX")
            FWD_RC=`echo {config[primers][fwd][sequence]} | tr '[ATUGCYRSWKMBDHNatugcyrswkbdhvn]' '[TAACGRYSWMKVHDBNtaacgryswmkvhdbn]' |rev`
            RVS_RC=`echo {config[primers][rvs][sequence]} | tr '[ATUGCYRSWKMBDHNatugcyrswkbdhvn]' '[TAACGRYSWMKVHDBNtaacgryswmkvhdbn]' |rev`

            {config[dada_bin]}/cutadapt -g {config[primers][fwd][sequence]} \
             --no-indels -n {config[primer_cutting][count]} -O {config[primer_cutting][overlap]} \
              -m 1\
             -j {threads} -e {config[primer_cutting][perc_mismatch]} \
             --untrimmed-output=$TMPD/{wildcards.library}.unt.fastq \
             -o $TMPD/{wildcards.library}.fastq {input} &> {log}
            
            {config[dada_bin]}/cutadapt -a $RVS_RC \
             --no-indels -n {config[primer_cutting][count]} \
              -m 1 \
             -j {threads} -e {config[primer_cutting][perc_mismatch]} \
             -o {output} $TMPD/{wildcards.library}.fastq >> {log} 2>&1

            {config[dada_bin]}/cutadapt -g {config[primers][rvs][sequence]} \
             --no-indels -n {config[primer_cutting][count]} -O {config[primer_cutting][overlap]} \
              -m 1\
             -j {threads} -e {config[primer_cutting][perc_mismatch]} --trimmed-only \
             -o $TMPD/{wildcards.library}.tr2.fastq \
             $TMPD/{wildcards.library}.unt.fastq >> {log} 2>&1

            {config[dada_bin]}/cutadapt -a $FWD_RC \
             --no-indels -n {config[primer_cutting][count]} \
              -m 1 \
             -j {threads} -e {config[primer_cutting][perc_mismatch]} \
             -o $TMPD/{wildcards.library}.final.fastq \
             $TMPD/{wildcards.library}.tr2.fastq  >> {log} 2>&1

            cat $TMPD/{wildcards.library}.final.fastq >> {output}
            """



