postConts = []
if config['hand_off']['phyloseq']:
    postConts.append("sequenceTables/all.seqTab.phyloseq.RDS")
if config['postprocessing']['treeing']['do']:
    postConts.append("post/tree.newick")
if config['postprocessing']['rarefaction_curve']:
    postConts.append("stats/rarefaction_curves.pdf")

rule post_control_noFilter:
    input:
        postConts
    output:
        "postprocessing.done"
    threads: 1
    params:
        runtime="00:10:00",
        mem="8G"
    shell:
        """
        touch {output}
        """

rule rarefaction_curve_noFilter:
    input:
        "sequenceTables/all.seqTab.RDS",
        "reporting/finalNumbers_perSample.tsv"
    output:
        "stats/rarefaction_curves.pdf"
    threads: 1
    params:
        mem="8G",
        runtime="12:00:00"
    conda: "dada_env_test.yml"
    log: "logs/rarefaction_curve.log"
    script:
        srcdir("dada_scripts/rarefaction_curve.R")



rule guilds_noFilter:
    input:
        "sequenceTables/all.seqTab.tax.tsv"
    output:
        "post/all.seqTab.guilds.tsv"
    threads: 1
    params:
        mem="8G",
        runtime="48:00:00"
    log: "logs/funguild.log"
    conda: "dada_env_test.yml"
    message: "Running funguild on {input}."
    shell:
        """
        {config[dada_src]}/Guilds_v1.1.local.2.py -otu {input} -output {output} -path_to_db {config[postprocessing][funguild][funguild_db]} &> {log}
        """

#rule fapro_noFilter:
#    input:
#        "sequenceTables/all.seqTab.tax.tsv"
#    output:
#        temp("post/fapro.tsv"),
#        "post/all.seqTab.fapro.tsv"
#    threads: 1
#    params:
#        mem="8G",
#        runtime="48:00:00", 
#        sub=7 if config['taxonomy']['look_for_species'] else 6
#    log: "logs/faprotax.log"
#    conda: "dada_env_test.yml"
#    message: "Running faprotax on {input}."
#    shell:
#        """
#        NCOL=$((`awk '{{print NF}}' {input} | sort -nu | tail -n 1` -1))
#        ICOL=$((NCOL-{params.sub}))
#        IGCOLS=`eval echo {{$ICOL..$NCOL}} | sed 's/ /,/g'`
#        {config[dada_root]}/dada_scripts/collapse_table.py -i {input} \
#         --omit_unrepresented_groups --out_groups2records_table {output[0]} -g {config[Faprotax_DB]}\
#         -d taxonomy --column_names_are_in first_data_line --omit_columns 0,$IGCOLS
#        paste {input} <(awk '{{if($1 !~ /^#/){{if($1 ~ /^record$/) {{for (i = 2; i <= NF; ++i) {{a[i]=$i}};\
#         print "FAPROTAX"}}else {{ b = "";\
#         for (i = 2; i <= NF; ++i) {{if($i == "1") {{b = b a[i] ";" }}}};\
#         print b}}}}}}' {output[0]}) >> {output[1]}
#        """

if config['hand_off']['phyloseq']:
    physInputs = ["sequenceTables/all.seqTab.RDS","reporting/finalNumbers_perSample.tsv"]
    if config['postprocessing']['treeing']['do']:
        physInputs.append("post/tree.newick")
    rule phyloseq_handoff_post:
        input:
            physInputs
        output:
            "sequenceTables/all.seqTab.phyloseq.RDS"
        threads: 1
        params:
            currentStep = "post",
            mem="8G",
            runtime="12:00:00"
        conda: "dada_env_test.yml"
        log: "logs/phyloseq_hand-off.log"
        script:
            srcdir("dada_scripts/phyloseq_handoff.R")


if config['postprocessing']['treeing']['fasttreeMP'] != "":
    rule treeing_noFilter_fasttreeMP:
        input:
            "sequenceTables/all.seqs.fasta"
        output:
            "post/all.seqs.multi.fasta",
            "post/tree.newick"
        threads: 10
        params:
            mem="8G",
            runtime="12:00:00"
        conda: "dada_env_test.yml"
        log: "logs/treeing.log"
        shell:
            """
            clustalo -i {input} -o {output[0]} --outfmt=fasta --threads={threads} --force
            {config[postprocessing][treeing][fasttreeMP]} -nt -gamma -no2nd -fastest -spr 4 \
             -log {log} -quiet {output[0]} > {output[1]}
            """
else:
    rule treeing_noFilter:
        input:
            "sequenceTables/all.seqs.fasta"
        output:
            "post/all.seqs.multi.fasta",
            "post/tree.newick"
        threads: 1
        params:
            mem="8G",
            runtime="48:00:00"
        conda: "dada_env_test.yml"
        log: "logs/treeing.log"
        shell:
            """
            clustalo -i {input} -o {output[0]} --outfmt=fasta --threads={threads} --force
            fasttree -nt -gamma -no2nd -fastest -spr 4 \
             -log {log} -quiet {output[0]} > {output[1]}
            """




#rule panFP:

#rule pieCrust:

#tax4fun

#label non-target, non-ITSx, 

#if config['keep_target_taxa'] == ".":
