rule dada_control:
    input:
        "sequenceTables/all.seqTab.RDS",
        "sequenceTables/all.seqs.fasta",
        expand("stats/QC_{step}.{run}.{direction}.pdf",step=['1','filtered'],direction=['fwd','rvs'],run=samples.run.unique())  
    output:
        "dada.done"
    threads: 1
    params:
        runtime="00:10:00",
        mem="8G"
    shell:
        """
        touch {output}
        """

# add rule to gather stats from filtering 

#def get_processed_fastq_perRun(wildcards):
#    runSamples=samples.loc[samples['run']==int(wildcards.run)].dropna()
#    return "preprocessing/"+wildcards.run+"/"+samples.loc[samples['run']==int(wildcards.run), "library"].dropna()+".fwd.fastq"

def get_sample_perRun(wildcards,prefix,suffix):
    return prefix+samples.loc[samples['run']==wildcards.run, "sample"].unique()+suffix

rule filter_numbers:
    input:
        "reporting/primerNumbers_perSample.tsv",
        expand("filtered/{samples.run}/{samples.sample}.{direction}.fastq.gz", samples=samples.itertuples(), direction=["fwd","rvs"])
    output:
        report("reporting/filteredNumbers.tsv",category="Reads")
    threads: 1
    params:
        currentStep = "filtered",
        mem="8G",
        runtime="12:00:00"
    log: "logs/countInputReads.log"
    script:
        "{config[dada_src]}/report_readNumbers.R"

rule dada_qc1:
    input:
        lambda wildcards: get_sample_perRun(wildcards,"preprocessing/{run}/",".fwd.fastq"),
        lambda wildcards: get_sample_perRun(wildcards,"preprocessing/{run}/",".rvs.fastq")
    output:
        report("stats/QC_1.{run}.fwd.pdf"),
        report("stats/QC_1.{run}.rvs.pdf")
    threads: 1
    params:
        path="preprocessing/{run}",
        mem="8G",
        runtime="12:00:00"
    log: "logs/DADA2_QC_1.{run}.log"
    message: "Running QC on {params.path}."
    script:
        "{config[dada_src]}/dada_QC.R"

rule dada_qc_filtered:
    input:
        lambda wildcards: get_sample_perRun(wildcards,"filtered/{run}/",".fwd.fastq.gz"),
        lambda wildcards: get_sample_perRun(wildcards,"filtered/{run}/",".rvs.fastq.gz")
    output:
        report("stats/QC_filtered.{run}.fwd.pdf"),
        report("stats/QC_filtered.{run}.rvs.pdf")
    threads: 1
    params:
        path="filtered/{run}",
        mem="8G",
        runtime="12:00:00"
    log: "logs/DADA2_QC_filtered.{run}.log"
    message: "Running QC on {params.path}."
    script:
        "{config[dada_src]}/dada_QC.R"

rule dada_filter:
    input:
        "preprocessing/{run}/{sample}.fwd.fastq",
        "preprocessing/{run}/{sample}.rvs.fastq"
    output:
        "filtered/{run}/{sample}.fwd.fastq.gz",
        "filtered/{run}/{sample}.rvs.fastq.gz"
    threads: 1
    params:
        mem="8G",
        runtime="12:00:00"
    log: "logs/DADA2_filtering.{run}.{sample}.log"
    message: "Running filtering on {input}."
    script:
        "{config[dada_src]}/dada_filter.R"


rule dada_errors:
    input:
        lambda wildcards: get_sample_perRun(wildcards,"filtered/{run}/",".{direction}.fastq.gz")
    output:
        "errors/models.{run}.{direction}.RDS",
        "stats/error_models.{run}.{direction}.pdf",
    threads: 1
    params:
        mem="8G",
        runtime="120:00:00"
    log: "logs/DADA2_errors.{run}.{direction}.log"
    message: "Running error models on {input}."
    script:
        "{config[dada_src]}/dada_errors.R"

rule dada_mergeReadPairs:
    input:
        "errors/models.{run}.fwd.RDS",
        "errors/models.{run}.rvs.RDS",
        "filtered/{run}/{sample}.fwd.fastq.gz",
        "filtered/{run}/{sample}.rvs.fastq.gz"
    output:
        "merged/{run}/{sample}.RDS"
    threads: 1
    params:
        mem="8G",
        runtime="24:00:00"
    log: "logs/DADA2_mergeReadPairs.{run}.{sample}.log"
    message: "merging reads for {wildcards.run} {wildcards.sample}."
    script:
        "{config[dada_src]}/dada_onlyMergeReads.R"

rule dada_mergeSamples:
    input:
        lambda wildcards: get_sample_perRun(wildcards,"merged/{run}/",".RDS"),
    output:
        "merged/dada_merged.{run}.RDS",
        "sequenceTables/seqTab.{run}.RDS",
        "sequenceTables/seqTab.{run}.tsv"
    threads: 1
    params:
        mem="30G,highmem",
        runtime="120:00:00"
    log: "logs/DADA2_mergeSamples.{run}.log"
    message: "preparing sequence table for {wildcards.run}."
    script:
        "{config[dada_src]}/dada_gatherMergedReads.R"

if config["chimeras"]["remove"]:
    rule dada_mergeruns:
        input:
            expand("sequenceTables/seqTab.{run}.RDS",run=samples.run.unique())
        output:
            "sequenceTables/all.seqTab.RDS",
            "sequenceTables/all.seqs.fasta",
            "sequenceTables/all.seqTab.tsv",
            "sequenceTables/pre_chimera.seqTab.RDS",
            "sequenceTables/pre_chimera.seqs.fasta",
            "sequenceTables/pre_chimera.seqTab.tsv"
        threads: 1
        params:
            mem="8G",
            runtime="120:00:00"
        log: "logs/DADA2_mergeRuns.log"
        message: "merging runs and removing chimeras for {input}."
        script:
            "{config[dada_src]}/dada_mergeRuns.R"
else:
    rule dada_mergeruns:
        input:
            expand("sequenceTables/seqTab.{run}.RDS",run=samples.run.unique())
        output:
            "sequenceTables/all.seqTab.RDS",
            "sequenceTables/all.seqs.fasta",
            "sequenceTables/all.seqTab.tsv"
        threads: 1
        params:
            mem="8G",
            runtime="120:00:00"
        log: "logs/DADA2_mergeRuns.log"
        message: "merging runs for {input}."
        script:
            "{config[dada_src]}/dada_mergeRuns.R"


